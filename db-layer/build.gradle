apply plugin: 'java'
apply plugin: 'nu.studer.jooq'

apply from: '../credentials.gradle'

sourceCompatibility = 1.8
targetCompatibility = 1.8

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'nu.studer:gradle-jooq-plugin:1.0.5'
        classpath 'mysql:mysql-connector-java:6.0.2'
    }

    configurations.classpath {
        resolutionStrategy {
            forcedModules = [
                    'org.jooq:jooq:3.8.0',
                    'org.jooq:jooq-meta:3.8.0',
                    'org.jooq:jooq-codegen:3.8.0'
            ]
        }
    }
}

repositories {
    jcenter()
}

dependencies {
    compile project(':core')
    compile 'org.jooq:jooq:3.8.0'
    compile 'mysql:mysql-connector-java:6.0.2'
    compile 'org.apache.commons:commons-dbcp2:2.1.1'

    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:2.0.53-beta'
}

jooq {
    reports(sourceSets.main) {
        jdbc {
            driver = 'com.mysql.cj.jdbc.Driver'
            url = getDatabaseConnectionString()
            user = getDatabaseUsername()
            password = getDatabasePassword()
            schema = 'reports'
            properties {
                property {
                    key = 'serverTimezone'
                    value = 'UTC'
                }
                property {
                    key = 'ssl'
                    value = 'true'
                }
            }
        }
        generator {
            name = 'org.jooq.util.DefaultGenerator'
            strategy {
                name = 'org.jooq.util.DefaultGeneratorStrategy'
            }
            database {
                name = 'org.jooq.util.mysql.MySQLDatabase'
                inputSchema = 'reports'
            }
            generate {
                relations = true
                deprecated = false
                records = true
                immutablePojos = true
                fluentSetters = true
            }
            target {
                packageName = 'com.novoda.github.reports.data.db'
            }
        }
    }
}
